# - Config file for the Falaise package
# It defines the following variables
#
#  Falaise_VERSION          - Falaise version string
#  Falaise_VERSION_REVISION - Falaise development tag
#  Falaise_CMAKE_CONFIG_DIR - Falaise CMake config directory
#  Falaise_developer_tools_FOUND
#                           - Falaise developer tools availability flag
#  Falaise_companions_FOUND - Falaise companions availability flag
#  Falaise_docs_FOUND       - Falaise documentation availability flag
#  Falaise_RESOURCE_DIR     - Falaise's base resources directory
#
#  Falaise_DOXYGEN_HTML_DIR - path to directory holding Doxygen docs
#                            for Falaise. Empty if docs were not built
#  Falaise_DOXYGEN_TAGFILE  - path to Doxygen tagfile for Falaise's docs
#                            Empty if docs were not built
#

#----------------------------------------------------------------------
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#----------------------------------------------------------------------

# - Core API version
set(Falaise_VERSION "@Falaise_VERSION@")
# - Development Tag
set(Falaise_VERSION_REVISION "@Falaise_VERSION_REVISION@")
# - Falaise cmake-config path is stored to help explicit path resolution in client packages:
set(Falaise_CMAKE_CONFIG_DIR "${CMAKE_CURRENT_LIST_DIR}")

# Component Availability
set(Falaise_companions_FOUND "@FALAISE_WITH_COMPANIONS@")
set(Falaise_docs_FOUND "@FALAISE_WITH_DOCS@")

#-----------------------------------------------------------------------
# Start of CMake Generated
@PACKAGE_INIT@
# END OF CMAKE GENERATED

#-----------------------------------------------------------------------
# Setup paths that clients may need
#
# set_and_check(Falaise_INCLUDE_DIR  "@PACKAGE_CMAKE_INSTALL_INCLUDEDIR@")
# message( STATUS "Falaise_INCLUDE_DIR  = ${Falaise_INCLUDE_DIR}")

set_and_check(Falaise_RESOURCE_DIR "@PACKAGE_CMAKE_INSTALL_DATAROOTDIR@/@FALAISE_RESOURCE_DIR@")
message( STATUS "Falaise_RESOURCE_DIR = ${Falaise_RESOURCE_DIR}")

#-----------------------------------------------------------------------
# Handle paths/targets for third party deps here...
# Our main dependency is Bayeux, and this will supply the main third
# party deps for Falaise as well
set(FALAISE_BAYEUX_VERSION @Bayeux_VERSION@)
# Force the Bayeux config path to the one used for Falaise build
set(Bayeux_DIR @Bayeux_CMAKE_CONFIG_DIR@)
message( STATUS "Searching Bayeux ${FALAISE_BAYEUX_VERSION} from ${Bayeux_DIR} ...")
find_package(Bayeux ${FALAISE_BAYEUX_VERSION} EXACT REQUIRED)

#-----------------------------------------------------------------------
# Include the file listing all the imported targets.
# This is installed in the same location as us...
#
if(NOT Falaise_TARGETS_LOADED)
  include("${CMAKE_CURRENT_LIST_DIR}/FalaiseTargets.cmake")
  set(Falaise_TARGETS_LOADED 1)
endif()

#-----------------------------------------------------------------------
# Documentation - loaded optionally as it's only present if docs were
# built
#
include("${CMAKE_CURRENT_LIST_DIR}/FalaiseDocs.cmake" OPTIONAL)
